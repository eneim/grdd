group 'im.ene'
version '1.0-SNAPSHOT'

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.github.jengelman.gradle.plugins:shadow:5.0.0'
  }
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8

ext {
  // default_test_class is defined in gradle.properties
  // note that: if running system has the same field default_test_class in gradle.properties,
  // it will be used instead of our custom value.
  defaultTestClass = default_test_class
}

sourceSets {
  behaviorTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/behaviorTest/java')
    }
    resources {
      srcDir file('src/behaviorTest/resources')
    }
  }
}

// Always show the result of every unit test, even if it passes.
test {
  testLogging {
    events "passed", "skipped", "failed", "standardOut", "standardError"
  }
}

shadowJar {
  archiveBaseName = 'grdd'
  version = '1.0.0'
}

task cucumber() {
  dependsOn assemble, compileJava, compileBehaviorTestJava
  doLast {
    javaexec {
      main = "cucumber.api.cli.Main"
      classpath = configurations.behaviorTestRuntime + sourceSets.main.output +
              sourceSets.behaviorTest.output
      args = ['--plugin', 'pretty', '--glue', 'im.ene.grdd', 'src/behaviorTest/resources']
    }
  }
}

task testValueFromProperties() {
  // here we retrieve defaultTestClass value from ext object
  println("${project.ext.defaultTestClass}")
}

// Ensure the cucumber tests are executed as part of the build. Makes for a very pretty output.
build.dependsOn cucumber

repositories {
  mavenCentral()
}

//configurations {
//  behaviorTestCompile.extendsFrom testCompile
//  behaviorTestRuntime.extendsFrom testRuntime
//}

dependencies {
  testCompile group: 'junit', name: 'junit', version: '4.12'
  behaviorTestCompile 'info.cukes:cucumber-java:1.2.4'
  behaviorTestCompile 'info.cukes:cucumber-junit:1.2.4'
}
